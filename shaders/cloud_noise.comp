#version 430 core

layout(local_size_x = 4, local_size_y = 4, local_size_z = 4) in;

layout(binding = 0, rgba16f) uniform image3D coverageLut;
layout(binding = 1, rgba16f) uniform image3D shapeLut;

uniform ivec3 volumeSize;

float hash(vec3 p) {
    p = fract(p * 0.3183099 + vec3(0.1));
    p *= 17.0;
    return fract(p.x * p.y * p.z * (p.x + p.y + p.z));
}

float noise(vec3 p) {
    vec3 i = floor(p);
    vec3 f = fract(p);
    float n000 = hash(i + vec3(0,0,0));
    float n001 = hash(i + vec3(0,0,1));
    float n010 = hash(i + vec3(0,1,0));
    float n011 = hash(i + vec3(0,1,1));
    float n100 = hash(i + vec3(1,0,0));
    float n101 = hash(i + vec3(1,0,1));
    float n110 = hash(i + vec3(1,1,0));
    float n111 = hash(i + vec3(1,1,1));
    vec3 u = f * f * (3.0 - 2.0 * f);
    return mix(
        mix(mix(n000, n100, u.x), mix(n010, n110, u.x), u.y),
        mix(mix(n001, n101, u.x), mix(n011, n111, u.x), u.y),
        u.z
    );
}

float fbm(vec3 p) {
    float v = 0.0;
    float a = 0.5;
    for (int i = 0; i < 5; i++) {
        v += a * noise(p);
        p *= 2.0;
        a *= 0.5;
    }
    return v;
}

void main() {
    ivec3 gid = ivec3(gl_GlobalInvocationID.xyz);
    if (any(greaterThanEqual(gid, volumeSize))) {
        return;
    }

    vec3 coord = (vec3(gid) + 0.5) / vec3(volumeSize);
    vec3 p = coord * 2.0 - 1.0;

    float bands = fbm(p * 3.1 + vec3(1.7, -2.2, 0.5));
    float streaks = fbm(p * 7.2 + vec3(-4.1, 2.6, 3.3));
    float detail = fbm(p * 11.3 + vec3(5.1, -3.7, 2.6));
    imageStore(coverageLut, gid, vec4(bands, streaks, detail, 1.0));

    float base = fbm(p * 22.0 + vec3(8.2, 1.7, -3.4));
    float detailShape = fbm(p * 64.0 - vec3(4.1, 5.6, 2.0));
    float billow = abs(fbm(p * 12.0 + vec3(-6.0, 2.5, 4.3)) * 2.0 - 1.0);
    imageStore(shapeLut, gid, vec4(base, detailShape, billow, 1.0));
}
