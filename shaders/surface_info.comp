#version 430 core

#include "planet_common.glsl"

layout(local_size_x = 1, local_size_y = 1, local_size_z = 1) in;

uniform vec3 queryPosition;
uniform float planetRadius;
uniform float heightScale;
uniform float seaLevel;
uniform mat3 worldToPlanet;
uniform float minAltitudeOffset;

layout(std430, binding = 0) buffer SurfaceInfo {
    vec4 data0; // xyz = surface normal, w = surface radius
    vec4 data1; // x = altitude, y = terrain height, z = clamped surface radius, w = water flag
};

void main() {
    vec3 planetPos = worldToPlanet * queryPosition;
    float terrainHeightValue = terrainHeight(planetPos, planetRadius, heightScale);

    float terrainSurfaceRadius = planetRadius + terrainHeightValue;
    float waterSurfaceRadius = planetRadius + seaLevel;
    bool waterCovered = terrainHeightValue <= seaLevel;

    float surfaceRadius = waterCovered ? waterSurfaceRadius : terrainSurfaceRadius;
    float clampedSurfaceRadius = surfaceRadius + max(minAltitudeOffset, 0.0);

    float dist = length(queryPosition);
    float altitude = dist - surfaceRadius;

    vec3 surfaceNormal = dist > 0.0 ? queryPosition / dist : vec3(0.0, 1.0, 0.0);
    if (!waterCovered) {
        float d0 = planetSDF(planetPos, planetRadius, heightScale);
        surfaceNormal = computeNormal(planetPos, d0, planetRadius, heightScale);
    }

    data0 = vec4(surfaceNormal, surfaceRadius);
    data1 = vec4(altitude, terrainHeightValue, clampedSurfaceRadius, waterCovered ? 1.0 : 0.0);
}
